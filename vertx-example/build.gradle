plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

repositories {
    jcenter()
}

version = '3.2.1'
sourceCompatibility = '1.8'

dependencies {
    compile project(':vertx')
    compile project(':logging')
}

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'demo.MyApp'

// Vert.x watches for file changes in all subdirectories
// of src/ but only for files with .java extension
def watchForChange = 'src/**/*.java'

// Vert.x will call this task on changes
def doOnChange = './gradlew classes'

run {
    args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

shadowJar {
    classifier = 'fat'
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}
